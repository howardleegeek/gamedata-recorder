; GameData Recorder Installer Script
; NSIS Modern User Interface

!define PRODUCT_NAME "GameData Recorder"
!define PRODUCT_VERSION "${VERSION}"
!define PRODUCT_VERSION_RAW "${VERSION_RAW}"
!define PRODUCT_PUBLISHER "GameData Labs"
!define PRODUCT_WEB_SITE "https://gamedatalabs.com/"
!define APP_UUID "a1b2c3d4-e5f6-7890-abcd-ef1234567890"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\GameData Recorder.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APP_UUID}"
!define PRODUCT_UNINST_ROOT_KEY "HKCU"

; MUI Settings
!include "MUI2.nsh"
!include "FileFunc.nsh"
!include "WinMessages.nsh"

!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"
!define MUI_HEADERIMAGE
!define MUI_HEADERIMAGE_BITMAP "${NSISDIR}\Contrib\Graphics\Header\nsis3-grey.bmp"
!define MUI_WELCOMEFINISHPAGE_BITMAP "${NSISDIR}\Contrib\Graphics\Wizard\nsis3-grey.bmp"

; Welcome page
!insertmacro MUI_PAGE_WELCOME

; License page
!define MUI_LICENSEPAGE_CHECKBOX
!insertmacro MUI_PAGE_LICENSE "..\LICENSE"

; Directory page
!insertmacro MUI_PAGE_DIRECTORY

; Instfiles page
!insertmacro MUI_PAGE_INSTFILES

; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\GameData Recorder.exe"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; Installer attributes
Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "..\dist\GameData-Recorder-Setup-${PRODUCT_VERSION}.exe"
InstallDir "$LOCALAPPDATA\Programs\GameData Recorder"
InstallDirRegKey HKCU "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show
RequestExecutionLevel user

; Version Information
!define VI_PRODUCT_VERSION "${PRODUCT_VERSION_RAW}.0"
VIProductVersion "${VI_PRODUCT_VERSION}"
VIAddVersionKey "ProductName" "${PRODUCT_NAME}"
VIAddVersionKey "CompanyName" "${PRODUCT_PUBLISHER}"
VIAddVersionKey "FileVersion" "${PRODUCT_VERSION}"
VIAddVersionKey "ProductVersion" "${PRODUCT_VERSION}"
VIAddVersionKey "FileDescription" "${PRODUCT_NAME} Installer"
VIAddVersionKey "LegalCopyright" "Copyright © 2025 ${PRODUCT_PUBLISHER}"

; Function to check if previous versions of owl-control exist, and run uninstaller that will maintain data_dump folder
Function .onInit
  ; Check if already installed
  ReadRegStr $0 ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString"
  StrCmp $0 "" done

  ; Found existing installation
  MessageBox MB_OKCANCEL|MB_ICONQUESTION \
    "${PRODUCT_NAME} is already installed. $\n$\nClick 'OK' to remove the previous version and continue, or 'Cancel' to cancel this installation." \
    IDOK uninst
  Abort

uninst:
  ; Clear errors
  ClearErrors

  ; Run the uninstaller silently using the UninstallString
  ExecWait '$0 /S _?=$INSTDIR' $1

  ; Check if uninstaller was successful (exit code 0)
  IntCmp $1 0 success
    ; Uninstaller failed
    Abort

success:
  ; Delete the uninstaller after it finishes
  Delete $0

done:
FunctionEnd

Section "MainSection" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer

  ; Install Visual C++ Redistributable if needed
  ${ifNot} ${FileExists} "$SYSDIR\msvcp140.dll"
    DetailPrint "Installing Visual C++ Redistributable..."
    File /oname=$PLUGINSDIR\vc_redist.x64.exe "downloads\vc_redist.x64.exe"
    ExecWait '"$PLUGINSDIR\vc_redist.x64.exe" /norestart'
  ${endIf}

  ; Copy all files and folders from dist directory
  File /r /x "GameData-Recorder-Setup-*.exe" "..\dist\*.*"
  File "gamedata-logo.ico"

  ; Create shortcuts
  CreateDirectory "$SMPROGRAMS\${PRODUCT_NAME}"
  CreateShortcut "$SMPROGRAMS\${PRODUCT_NAME}\${PRODUCT_NAME}.lnk" "$INSTDIR\GameData Recorder.exe" "" "$INSTDIR\gamedata-logo.ico" 0
  CreateShortcut "$DESKTOP\${PRODUCT_NAME}.lnk" "$INSTDIR\GameData Recorder.exe" "" "$INSTDIR\gamedata-logo.ico" 0
  CreateShortcut "$SMPROGRAMS\${PRODUCT_NAME}\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -AdditionalIcons
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortcut "$SMPROGRAMS\${PRODUCT_NAME}\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"

  ; Registry keys
  WriteRegStr HKCU "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\GameData Recorder.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\GameData Recorder.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "InstallLocation" "$INSTDIR"

  ; Get installation size
  ${GetSize} "$INSTDIR" "/S=0K" $0 $1 $2
  IntFmt $0 "0x%08X" $0
  WriteRegDWORD ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "EstimatedSize" "$0"
SectionEnd

; Uninstaller
Function un.onUninstSuccess
  HideWindow
  ${IfNot} ${Silent}
    MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer. Your existing recordings were not removed and can be found in the installation directory."
  ${EndIf}
FunctionEnd

; Function to check if GameData Recorder is running by checking for the mutex
Function un.CheckIfGameDataRecorderRunning
  ; Try to open the mutex that GameData Recorder creates
  System::Call 'kernel32::OpenMutexW(i 0x100000, i 0, w "GameData-Recorder-SingleInstance") i .R0'
  IntCmp $R0 0 not_running
    ; Mutex exists, application is running
    StrCpy $0 1
    Goto done
  not_running:
    ; Mutex doesn't exist, application is not running
    StrCpy $0 0
  done:
FunctionEnd

Function un.onInit
  ; Check if GameData Recorder is running
  Call un.CheckIfGameDataRecorderRunning
  IntCmp $0 1 running
  Goto not_running

  running:
    MessageBox MB_ICONSTOP|MB_OK "GameData Recorder is currently running. Please close the application before uninstalling." IDOK
    Abort

  not_running:
    ${IfNot} ${Silent}
      MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
      Abort
    ${EndIf}
FunctionEnd

Section Uninstall
  ; Remove shortcuts first
  Delete "$SMPROGRAMS\${PRODUCT_NAME}\Uninstall.lnk"
  Delete "$SMPROGRAMS\${PRODUCT_NAME}\Website.lnk"
  Delete "$SMPROGRAMS\${PRODUCT_NAME}\${PRODUCT_NAME}.lnk"
  Delete "$DESKTOP\${PRODUCT_NAME}.lnk"
  RMDir "$SMPROGRAMS\${PRODUCT_NAME}"

  ; Remove all subdirectories except data_dump
  RMDir /r "$INSTDIR\resources"
  RMDir /r "$INSTDIR\assets"
  RMDir /r "$INSTDIR\data"
  RMDir /r "$INSTDIR\iconengines"
  RMDir /r "$INSTDIR\obs-plugins"
  RMDir /r "$INSTDIR\platforms"
  RMDir /r "$INSTDIR\rtmp-services"
  RMDir /r "$INSTDIR\styles"
  RMDir /r "$INSTDIR\text-freetype2"
  RMDir /r "$INSTDIR\win-capture"

  ; Remove all other files in root directory
  Delete "$INSTDIR\*.*"

  ; Try to remove the installation directory
  ; This will only succeed if empty or only contains data_dump
  RMDir "$INSTDIR"

  ; Remove registry keys
  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKCU "${PRODUCT_DIR_REGKEY}"

  SetAutoClose true
SectionEnd